# Day 7: Laboratories

```elixir
Mix.install([
  {:kino, "~> 0.18.0"}
])
```

## Description

You thank the cephalopods for the help and exit the trash compactor, finding yourself in the familiar halls of a North Pole research wing.

Based on the large sign that says "teleporter hub", they seem to be researching teleportation; you can't help but try it for yourself and step onto the large yellow teleporter pad.

Suddenly, you find yourself in an unfamiliar room! The room has no doors; the only way out is the teleporter. Unfortunately, the teleporter seems to be leaking magic smoke.

Since this is a teleporter lab, there are lots of spare parts, manuals, and diagnostic equipment lying around. After connecting one of the diagnostic tools, it helpfully displays error code 0H-N0, which apparently means that there's an issue with one of the tachyon manifolds.

```elixir
input = Kino.Input.textarea("Please paste your input file:")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
raw_input =
  Kino.Input.read(input)
  |> String.split("\n", trim: true)
```

## Part 1

You quickly locate a diagram of the tachyon manifold (your puzzle input). A tachyon beam enters the manifold at the location marked S; tachyon beams always move downward. Tachyon beams pass freely through empty space (.). However, if a tachyon beam encounters a splitter (^), the beam is stopped; instead, a new tachyon beam continues from the immediate left and from the immediate right of the splitter.

For example:

```
.......S.......
...............
.......^.......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............
```

In this example, the incoming tachyon beam (|) extends downward from S until it reaches the first splitter:

```
.......S.......
.......|.......
.......^.......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............
```

At that point, the original beam stops, and two new beams are emitted from the splitter:

```
.......S.......
.......|.......
......|^|......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............
```

Those beams continue downward until they reach more splitters:

```
.......S.......
.......|.......
......|^|......
......|.|......
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............
```

At this point, the two splitters create a total of only three tachyon beams, since they are both dumping tachyons into the same place between them:

```
.......S.......
.......|.......
......|^|......
......|.|......
.....|^|^|.....
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............
```

This process continues until all of the tachyon beams reach a splitter or exit the manifold:

```
.......S.......
.......|.......
......|^|......
......|.|......
.....|^|^|.....
.....|.|.|.....
....|^|^|^|....
....|.|.|.|....
...|^|^|||^|...
...|.|.|||.|...
..|^|^|||^|^|..
..|.|.|||.|.|..
.|^|||^||.||^|.
.|.|||.||.||.|.
|^|^|^|^|^|||^|
|.|.|.|.|.|||.|
```

To repair the teleporter, you first need to understand the beam-splitting properties of the tachyon manifold. In this example, a tachyon beam is split a total of 21 times.

Analyze your manifold diagram. How many times will the beam be split?

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule TachyonSimulator do
  def simulate(manifold) do
    start_col = find_start_col(manifold)
    width = String.length(hd(manifold))
    
    Enum.drop(manifold, 1)
    |> Enum.reduce({MapSet.new([start_col]), 0}, fn line, {beams, splits} ->
      process_line(line, beams, width, splits)
    end)
    |> elem(1)
  end

  def simulate_part2(manifold) do
    start_col = find_start_col(manifold)
    {result, _} = count_paths(manifold, 1, start_col, %{})
    result
  end

  defp find_start_col([first_line | _]), do: :binary.match(first_line, "S") |> elem(0)

  defp process_line(line, beams, width, split_count) do
    Enum.reduce(beams, {MapSet.new(), split_count}, fn col, {next_beams, splits} ->
      case :binary.at(line, col) do
        ?. -> {MapSet.put(next_beams, col), splits}
        ?^ when col > 0 and col < width - 1 -> 
          {next_beams |> MapSet.put(col - 1) |> MapSet.put(col + 1), splits + 1}
        ?^ when col > 0 -> {MapSet.put(next_beams, col - 1), splits + 1}
        ?^ when col < width - 1 -> {MapSet.put(next_beams, col + 1), splits + 1}
        _ -> {next_beams, splits}
      end
    end)
  end

  defp count_paths(manifold, row, col, memo) do
    key = {row, col}
    
    if Map.has_key?(memo, key) do
      {memo[key], memo}
    else
      if row >= length(manifold) do
        {1, Map.put(memo, key, 1)}
      else
        line = Enum.at(manifold, row)
        char = :binary.at(line, col)
        
        case char do
          ?. ->
            {result, new_memo} = count_paths(manifold, row + 1, col, memo)
            {result, Map.put(new_memo, key, result)}
          
          ?^ ->
            width = String.length(line)
            
            {left, memo1} =
              if col > 0,
                do: count_paths(manifold, row + 1, col - 1, memo),
                else: {0, memo}
            
            {right, memo2} =
              if col < width - 1,
                do: count_paths(manifold, row + 1, col + 1, memo1),
                else: {0, memo1}
            
            result = left + right
            {result, Map.put(memo2, key, result)}
          
          _ ->
            {0, Map.put(memo, key, 0)}
        end
      end
    end
  end
end
```

## Part 2

With your analysis of the manifold complete, you begin fixing the teleporter. However, as you open the side of the teleporter to replace the broken manifold, you are surprised to discover that it isn't a classical tachyon manifold - it's a quantum tachyon manifold.

With a quantum tachyon manifold, only a single tachyon particle is sent through the manifold. A tachyon particle takes both the left and right path of each splitter encountered.

Since this is impossible, the manual recommends the many-worlds interpretation of quantum tachyon splitting: each time a particle reaches a splitter, it's actually time itself which splits. In one timeline, the particle went left, and in the other timeline, the particle went right.

To fix the manifold, what you really need to know is the number of timelines active after a single particle completes all of its possible journeys through the manifold.

In the above example, there are many timelines. For instance, there's the timeline where the particle always went left:

```
.......S.......
.......|.......
......|^.......
......|........
.....|^.^......
.....|.........
....|^.^.^.....
....|..........
...|^.^...^....
...|...........
..|^.^...^.^...
..|............
.|^...^.....^..
.|.............
|^.^.^.^.^...^.
|..............
```

Or, there's the timeline where the particle alternated going left and right at each splitter:

```
.......S.......
.......|.......
......|^.......
......|........
......^|^......
.......|.......
.....^|^.^.....
......|........
....^.^|..^....
.......|.......
...^.^.|.^.^...
.......|.......
..^...^|....^..
.......|.......
.^.^.^|^.^...^.
......|........
```

Or, there's the timeline where the particle ends up at the same point as the alternating timeline, but takes a totally different path to get there:

```
.......S.......
.......|.......
......|^.......
......|........
.....|^.^......
.....|.........
....|^.^.^.....
....|..........
....^|^...^....
.....|.........
...^.^|..^.^...
......|........
..^..|^.....^..
.....|.........
.^.^.^|^.^...^.
......|........
```

In this example, in total, the particle ends up on 40 different timelines.

Apply the many-worlds interpretation of quantum tachyon splitting to your manifold diagram. In total, how many different timelines would a single tachyon particle end up on?

## Results

```elixir
defmodule Benchmark do
  def run(fun, runs \\ 100) do
    fun.()

    times =
      for _ <- 1..runs do
        {time, _result} = :timer.tc(fun)
        time
      end

    avg_time = Enum.sum(times) / runs
    min_time = Enum.min(times)
    max_time = Enum.max(times)

    {avg_time / 1000, min_time / 1000, max_time / 1000}
  end

  def format_time(ms) when ms < 0.001, do: "#{Float.round(ms * 1000, 3)} μs"
  def format_time(ms) when ms < 1, do: "#{Float.round(ms, 3)} ms"
  def format_time(ms) when ms < 1000, do: "#{Float.round(ms, 2)} ms"
  def format_time(ms), do: "#{Float.round(ms / 1000, 2)} s"
end

IO.puts("\n╔══════════════════════════════════════════════════════════╗")
IO.puts("║                        DAY 07                            ║")
IO.puts("╚══════════════════════════════════════════════════════════╝\n")

{time, result} = :timer.tc(fn -> TachyonSimulator.simulate(raw_input) end)
{avg, min, max} = Benchmark.run(fn -> TachyonSimulator.simulate(raw_input) end)

IO.puts("Part 1: #{result}")
IO.puts("  Time: #{Benchmark.format_time(time / 1000)}")
IO.puts("  Avg:  #{Benchmark.format_time(avg)} (min: #{Benchmark.format_time(min)}, max: #{Benchmark.format_time(max)})")

{time, result} = :timer.tc(fn -> TachyonSimulator.simulate_part2(raw_input) end)
{avg, min, max} = Benchmark.run(fn -> TachyonSimulator.simulate_part2(raw_input) end)

IO.puts("\nPart 2: #{result}")
IO.puts("  Time: #{Benchmark.format_time(time / 1000)}")
IO.puts("  Avg:  #{Benchmark.format_time(avg)} (min: #{Benchmark.format_time(min)}, max: #{Benchmark.format_time(max)})")
```
